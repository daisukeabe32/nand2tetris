// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Decoding
    And(a=instruction[15], b=true, out=instC); 
    Not(in=instC, out=instA);                  

    // WriteM
    And(a=instC, b=instruction[3], out=writeM);

    // ARegister
    Mux16(a=instruction, b=outALU, sel=instC, out=w);
    Or(a=instruction[5], b=instA, out=loadARegi);
    ARegister(in=w, load=loadARegi, out=outA, out[0..14]=addressM);

    // ALU
    And(a=instC, b=instruction[4], out=loadD);
    DRegister(in=outALU, load=loadD, out=outD);
    Mux16(a=outA, b=inM, sel=instruction[12], out=outAorM);
    ALU(x=outD, y=outAorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outALU, zr=zr, ng=ng);
    Mux16(a=false, b=outALU, sel=instC, out=outM);

    // JMP branches
        // not0, not-, + 
    Not(in=zr, out=notZr);
    Not(in=ng, out=notNg);
    And(a=notZr, b=notNg, out=po);
        // JGT, JEQ, JLT
    And(a=instruction[0], b=po, out=jgt);
    And(a=instruction[1], b=zr, out=jeq);
    And(a=instruction[2], b=ng, out=jlt);
        // Condition aggregation
    Or(a=jgt, b=jeq, out=jgteq);
    Or(a=jgteq, b=jlt, out=jmp);
    And(a=jmp, b=instC, out=loadJmp);

    // PC
    PC(in=outA, load=loadJmp, inc=true, reset=reset, out[0..14]=pc);

}